import dev.xpple.seedmapper.buildscript.CreateJavaBindingsTask
import dev.xpple.seedmapper.buildscript.GenerateBuildInfoTask
import net.fabricmc.loom.task.prod.ClientProductionRunTask

plugins {
	id 'fabric-loom' version "${fabric_loom_version}"
	id 'maven-publish'
}

base {
	archivesName = project.archives_base_name
	version = project.mod_version
	group = project.maven_group
}

loom {
	accessWidenerPath = file('src/main/resources/seedmapper.aw')

	runs {
		client {
			vmArgs '--enable-preview'
		}
	}
}

repositories {
	mavenCentral()
	maven {
		name = 'xpple'
		url = 'https://maven.xpple.dev/maven2'
	}
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = 'DJtheRedstoner'
		url = 'https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		officialMojangMappings {
			nameSyntheticMembers = true
		}
		parchment "org.parchmentmc.data:${project.parchment_mappings}"
	}
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
	productionRuntimeMods "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	include modImplementation("dev.xpple:clientarguments:${project.clientarguments_version}")
	include modImplementation("dev.xpple:betterconfig-fabric:${project.betterconfig_version}")
	include modImplementation("dev.xpple:simplewaypoints:${project.simplewaypoints_version}")

//	modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${project.devauth_version}") {
//		exclude group: 'net.fabricmc', module: 'fabric-loader'
//	}
//	productionRuntimeMods("me.djtheredstoner:DevAuth-fabric:${project.devauth_version}") {
//		exclude group: 'net.fabricmc', module: 'fabric-loader'
//	}

	testImplementation platform("org.junit:junit-bom:${project.junit_version}")
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def regenerateBindings = (project.findProperty("seedmapperRegenerateBindings")
		?: System.getenv("SEEDMAPPER_REGENERATE_BINDINGS")
		?: "false").toString().toBoolean()

tasks.register('createJavaBindings', CreateJavaBindingsTask) {
	onlyIf { regenerateBindings }
}

tasks.register('postProcessJavaBindings') {
	onlyIf { regenerateBindings }
	dependsOn 'createJavaBindings'
	doLast {
		def pattern = ~/(?<=\w)\$(?:get|set|slice)/
		fileTree('src/main/java/com/github/cubiomes').matching { include '**/*.java' }.files.each { file ->
			def original = file.getText('UTF-8')
			def updated = original.replaceAll(pattern, '')
			updated = updated.replaceAll(/(public\s+static\s+[^\s]+\s+)(\w+)\(MemorySegment seg, long index\)/, '$1$2AtIndex(MemorySegment seg, long index)')
			updated = updated.replaceAll(/(public\s+static\s+void\s+)(\w+)\(MemorySegment seg, long index,/, '$1$2AtIndex(MemorySegment seg, long index,')
			if (updated.contains('long sizeof()') && !updated.contains('MemorySegment asSlice(MemorySegment seg, long index)')) {
				def insertion = """
    public static MemorySegment asSlice(MemorySegment seg, long index) {
        return seg.asSlice(index*sizeof(), sizeof());
    }
"""
				updated = updated.replaceFirst(/(?s)\n\}\s*$/, insertion + "\n}")
			}
			if (updated != original) {
				file.write(updated, 'UTF-8')
			}
		}
	}
}
compileJava.dependsOn('postProcessJavaBindings')

tasks.register('generateBuildInfo', GenerateBuildInfoTask) {
	outputFile = new File(temporaryDir, "build_info.json")
}

tasks.register("prodClient", ClientProductionRunTask) {
	runDir = file("run")

	jvmArgs.add("-Ddevauth.enabled=true")
	jvmArgs.add("--enable-preview")

	javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

processResources {
	def props = [version: project.mod_version, minecraft_version_dependency: project.minecraft_version_dependency, loader_version: project.fabric_loader_version]
	inputs.properties props
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand props
	}

	from("LICENSE") {
		rename {"${it}_${project.base.archivesName.get()}"}
	}

	from("src/main/c/cubiomes/LICENSE") {
		rename {"${it}_cubiomes"}
	}

	from("src/main/c/cubiomes/loot/LICENSE_loot_library.h.txt")

	from generateBuildInfo
}

tasks.withType(JavaCompile).configureEach {
	it.options.compilerArgs += '--enable-preview'
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaExec).configureEach {
	it.jvmArgs '--enable-preview'
}

test {
	useJUnitPlatform()
	jvmArgs '--enable-preview'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.base.archivesName.get()
			from components.java
		}
	}

	repositories {

	}
}
